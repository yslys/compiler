/**********************************************************************
 Java CUP specification for a parser for C-- programs.
 **********************************************************************/

import java_cup.runtime.*;
import java.util.*;

/*
 * The code below redefines method syntax_error to give better error messages
 * than just "Syntax error".
 */
parser code {:

public void syntax_error(Symbol currToken) {
    if (currToken.value == null) {
        ErrMsg.fatal(0,0, "Syntax error at end of file");
    }
    else {
        ErrMsg.fatal(((TokenVal)currToken.value).linenum,
                     ((TokenVal)currToken.value).charnum,
                     "Syntax error");
    }
    System.exit(-1);
}
:};


/* Terminals (tokens returned by the scanner). */
terminal                INT;
terminal                BOOL;
terminal                VOID;
terminal TokenVal       TRUE;
terminal TokenVal       FALSE;
terminal                STRUCT;
terminal                CIN;
terminal                COUT;
terminal                IF;
terminal                ELSE;
terminal                WHILE;
terminal		        REPEAT;
terminal                RETURN;
terminal IdTokenVal     ID;
terminal IntLitTokenVal INTLITERAL;
terminal StrLitTokenVal STRINGLITERAL;
terminal                LCURLY;
terminal                RCURLY;
terminal                LPAREN;
terminal                RPAREN;
terminal                SEMICOLON;
terminal                COMMA;
terminal                DOT;
terminal                WRITE;
terminal                READ;
terminal                PLUSPLUS;
terminal                MINUSMINUS;
terminal                PLUS;
terminal                MINUS;
terminal                TIMES;
terminal                DIVIDE;
terminal                NOT;
terminal                AND;
terminal                OR;
terminal                EQUALS;
terminal                NOTEQUALS;
terminal                LESS;
terminal                GREATER;
terminal                LESSEQ;
terminal                GREATEREQ;
terminal                ASSIGN;


/* Non-terminals
 *
 * NOTE: You will need to add more non-terminals to this list as you
 *       add productions to the grammar below.
 */
non terminal ProgramNode      program;
non terminal LinkedList       declList;
non terminal DeclNode         decl;
non terminal LinkedList       varDeclList;
non terminal VarDeclNode      varDecl;
non terminal FnDeclNode       fnDecl;
non terminal StructDeclNode   structDecl;
non terminal LinkedList       structBody;
non terminal FormalsListNode  formals;
non terminal LinkedList       formalsList;
non terminal FormalDeclNode   formalDecl;
non terminal FnBodyNode       fnBody;
non terminal LinkedList       stmtList;
non terminal StmtNode         stmt;
non terminal AssignNode       assignExp;
non terminal ExpNode          exp;
non terminal ExpNode          term;
non terminal CallExpNode      fncall;
non terminal LinkedList       actualList;
non terminal ExpNode          loc;
non terminal TypeNode         type;
non terminal IdNode           id;






/* Precedences
 *
 * NOTE: Add precedence and associativity declarations here.
 *
 * http://web.deu.edu.tr/doc/oreily/java/langref/ch04_14.htm
 */
precedence right ASSIGN;
precedence left OR;
precedence left AND;
precedence nonassoc EQUALS, NOTEQUALS, LESS, GREATER, LESSEQ, GREATEREQ;
precedence left PLUS, MINUS;
precedence left TIMES, DIVIDE;
precedence right NOT;
// precedence left DOT;


start with program;


/* The grammar with actions
 *
 * NOTE: add more grammar rules below.
 *
 */
program         ::= declList: d
                {: 
                    RESULT = new ProgramNode(new DeclListNode(d));
                :}
                ;

declList        ::= declList:dl decl:d
                {: 
                    dl.addLast(d);
                    RESULT = dl;
                :}
                | /* epsilon */
                {: 
                    RESULT = new LinkedList<DeclNode>();
                :}
                ;

decl            ::= varDecl:v
                {: 
                    RESULT = v;
                :}
                | fnDecl:fn
                {:
                    RESULT = fn;
                :}
                | structDecl:st
                {:
                    RESULT = st;
                :}
                ;

varDeclList     ::= varDeclList:vl varDecl:v
                {:
                    // vl is followed by v; hence, use addLast()
                    vl.addLast(v);
                    RESULT = vl;
                :}
                | /* epsilon */
                {:
                    RESULT = new LinkedList<VarDeclNode>();
                :}
                ;

varDecl         ::= type:t id:i SEMICOLON
                {: 
                    RESULT = new VarDeclNode(t, i, VarDeclNode.NOT_STRUCT);
                :}
                | STRUCT id:id1 id:id2 SEMICOLON
                {:
                    // the last parameter is to distinguish from NOT_STRUCT
                    RESULT = new VarDeclNode(new StructNode(id1), id2, 0);
                :}
                ;

fnDecl          ::= type:t id:i formals:f fnBody:fb
                {:
                    RESULT = new FnDeclNode(t, i, f, fb);
                :}
                ;

structDecl      ::= STRUCT id:i LCURLY structBody:sb RCURLY SEMICOLON
                {:
                    RESULT = new StructDeclNode(i, new DeclListNode(sb));
                :}
                ;

structBody      ::= structBody:sb varDecl:v
                {:
                    sb.addLast(v);
                    RESULT = sb;
                :}
                | varDecl:v
                {:
                    LinkedList<VarDeclNode> ll = new LinkedList<>();
                    ll.addLast(v);
                    RESULT = ll;
                :}
                ;

formals         ::= LPAREN RPAREN
                {:
                    RESULT = new FormalsListNode(new LinkedList<FormalDeclNode>());
                :}
                | LPAREN formalsList:f RPAREN
                {:
                    RESULT = new FormalsListNode(f);
                :}
                ;

formalsList     ::= formalDecl:f
                {:
                    LinkedList<FormalDeclNode> ll = new LinkedList<>();
                    ll.addFirst(f);
                    RESULT = ll;
                :}
                | formalDecl:f COMMA formalsList:fl
                {:
                    fl.addFirst(f);
                    RESULT = fl;
                :}
                ;

formalDecl      ::= type:t id:i        // note: no struct parameters
                {:
                    RESULT = new FormalDeclNode(t, i);
                :}
                ;

fnBody          ::= LCURLY varDeclList:vl stmtList:sl RCURLY
                {:
                    RESULT = new FnBodyNode(new DeclListNode(vl), new StmtListNode(sl));
                :}
                ;

stmtList        ::= stmtList:sl stmt:s
                {:
                    sl.addLast(s);
                    RESULT = sl;
                :}
                | /* epsilon */
                {:
                    RESULT = new LinkedList<StmtNode>();
                :}
                ;

stmt            ::= assignExp:a SEMICOLON
                {:
                    RESULT = new AssignStmtNode(a);
                :}
                | loc:l PLUSPLUS SEMICOLON
                {:
                    RESULT = new PostIncStmtNode(l);
                :}
                | loc:l MINUSMINUS SEMICOLON
                {:
                    RESULT = new PostDecStmtNode(l);
                :}
                | CIN READ loc:l SEMICOLON
                {:
                    RESULT = new ReadStmtNode(l);
                :}
                | COUT WRITE exp:e SEMICOLON
                {:
                    RESULT = new WriteStmtNode(e);
                :}
                | IF LPAREN exp:e RPAREN LCURLY varDeclList:vl stmtList:sl RCURLY
                {:
                    RESULT = new IfStmtNode(e, new DeclListNode(vl), new StmtListNode(sl));
                :}
                | IF LPAREN exp:e RPAREN LCURLY varDeclList:vl1 stmtList:sl1 RCURLY ELSE LCURLY varDeclList:vl2 stmtList:sl2 RCURLY
                {:
                    RESULT = new IfElseStmtNode(e, new DeclListNode(vl1),
                          new StmtListNode(sl1), new DeclListNode(vl2),
                          new StmtListNode(sl2));
                :}
                | WHILE LPAREN exp:e RPAREN LCURLY varDeclList:vl stmtList:sl RCURLY
		        {:
                    RESULT = new WhileStmtNode(e, new DeclListNode(vl), new StmtListNode(sl));
                :}
                | REPEAT LPAREN exp:e RPAREN LCURLY varDeclList:vl stmtList:sl RCURLY
                {:
                    RESULT = new RepeatStmtNode(e, new DeclListNode(vl), new StmtListNode(sl));
                :}
                | RETURN exp:e SEMICOLON
                {:
                    RESULT = new ReturnStmtNode(e);
                :}
                | RETURN SEMICOLON
                {:
                    RESULT = new ReturnStmtNode(null);
                :}
                | fncall:f SEMICOLON
                {:
                    RESULT = new CallStmtNode(f);
                :}
                ;

assignExp       ::= loc:l ASSIGN exp:e
                {:
                    RESULT = new AssignNode(l, e);
                :}
                ;

// a node
exp             ::= assignExp:a
                {:
                    // assignExp already an AssignNode
                    RESULT = a; 
                :}
                | exp:a PLUS exp:b
                {:
                    RESULT = new PlusNode(a, b);
                :}
                | exp:a MINUS exp:b
                {:
                    RESULT = new MinusNode(a, b);      
                :}
                | exp:a TIMES exp:b
                {:
                    RESULT = new TimesNode(a, b);
                :}
                | exp:a DIVIDE exp:b
                {:
                    RESULT = new DivideNode(a, b);
                :}
                | NOT exp:e
                {:
                    RESULT = new NotNode(e);
                :}
                | exp:a AND exp:b
                {:
                    RESULT = new AndNode(a, b);
                :}
                | exp:a OR exp:b
                {:
                    RESULT = new OrNode(a, b);
                :}
                | exp:a EQUALS exp:b
                {:
                    RESULT = new EqualsNode(a, b);
                :}
                | exp:a NOTEQUALS exp:b
                {:
                    RESULT = new NotEqualsNode(a, b);
                :}
                | exp:a LESS exp:b
                {:
                    RESULT = new LessNode(a, b);
                :}
                | exp:a GREATER exp:b
                {:
                    RESULT = new GreaterNode(a, b);
                :}
                | exp:a LESSEQ exp:b
                {:
                    RESULT = new LessEqNode(a, b);
                :}
                | exp:a GREATEREQ exp:b
                {:
                    RESULT = new GreaterEqNode(a, b);
                :}
                | MINUS term:t
                {:
                    RESULT = new UnaryMinusNode(t);
                :}
                | term:t
                {:
                    // already a node
                    RESULT = t;
                :}
                ;

term            ::= loc:l
                {:
                    // already a node
                    RESULT = l;
                :}
                | INTLITERAL:i
                {:
                    RESULT = new IntLitNode(i.linenum, i.charnum, i.intVal);
                :}
                | STRINGLITERAL:s
                {:
                    RESULT = new StringLitNode(s.linenum, s.charnum, s.strVal);
                :}
                | TRUE:t
                {:
                    RESULT = new TrueNode(t.linenum, t.charnum);
                :}
                | FALSE:f
                {:
                    RESULT = new FalseNode(f.linenum, f.charnum);
                :}
                | LPAREN exp:e RPAREN
                {:
                    // exp already a node
                    RESULT = e;
                :}
                | fncall:f
                {:
                    // fncall already a node
                    RESULT = f;     
                :}
                ;

fncall          ::=  id:i LPAREN RPAREN   // fn call with no args
                {:
                    RESULT = new CallExpNode(i, null);
                :}
                | id:i LPAREN actualList:l RPAREN  // with args
                {:
                    // actualList is a LinkedList, not a node
                    RESULT = new CallExpNode(i, new ExpListNode(l));
                :}
                ;

actualList      ::= exp:e
                {:
                    LinkedList<ExpNode> ll = new LinkedList<>();
                    ll.addLast(e);
                    RESULT = ll;
                :}
                | actualList:l COMMA exp:e
                {:
                    l.addLast(e);
                    RESULT = l;
                :}
                ;

type            ::= INT
                {: 
                    RESULT = new IntNode();
                :}
                | BOOL
                {:
                    RESULT = new BoolNode();
                :}
                | VOID
                {:
                    RESULT = new VoidNode();
                :}
                ;

loc             ::= id:i
                {:
                    // already a node
                    RESULT = i;
                :}
                | loc:l DOT id:i
                {:
                    // loc and id already nodes
                    RESULT = new DotAccessExpNode(l, i);
                :}
                ;

id              ::= ID:i
                {: RESULT = new IdNode(i.linenum, i.charnum, i.idVal);
                :}
                ;
